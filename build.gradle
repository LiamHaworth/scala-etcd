allprojects {
    apply plugin: 'scala'
    apply plugin: 'java'
    apply plugin: 'maven'
    apply plugin: 'signing'

    repositories {
        mavenCentral()
    }

    group = "net.nikore.etcd"
    version = "0.6"
}


dependencies {
    compile 'org.scala-lang:scala-library:2.11.2'
    compile 'io.spray:spray-client_2.11:1.3.1'
    compile 'io.spray:spray-json_2.11:1.2.6'
    compile 'com.typesafe.akka:akka-actor_2.11:2.3.6'
}

task scaladocJar(type: Jar, dependsOn: scaladoc) {
    classifier = 'javadoc'
    from 'build/docs/scaladoc'
}

task sourcesJar(type: Jar) {
    from sourceSets.main.allSource
    classifier = 'sources'
}

artifacts {
    archives jar

    archives scaladocJar
    archives sourcesJar
}

uploadArchives {
    repositories {
        signing {
            sign configurations.archives
            mavenDeployer {
                beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

                repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                    authentication(userName: mavenUsername, password: mavenPassword)
                }

                pom.project {
                    name 'scala-etcd'
                    packaging 'jar'
                    description 'native scala client for etcd'
                    url 'https://github.com/nikore/scala-etcd'

                    scm {
                        url 'scm:git@github.com:nikore/scala-etcd.git'
                        connection 'scm:git@github.com:nikore/scala-etcd.git'
                        developerConnection 'scm:git@github.com:nikore/scala-etcd.git'
                    }

                    licenses {
                        license {
                            name 'The Apache Software License, Version 2.0'
                            url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                            distribution 'repo'
                        }
                    }

                    developers {
                        developer {
                            id 'nikore'
                            name 'Matt Christiansen'
                        }
                    }
                }
            }
        }
    }
}
